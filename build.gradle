apply from: 'dependencies.gradle'
apply from: 'checkstyle.gradle'

buildscript {
    ext.kotlinVersion = '1.4.32'
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'gradle.plugin.org.codehaus.groovy:groovy-android-gradle-plugin:3.0.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

allprojects {
    apply plugin: 'com.github.ben-manes.versions'
    
    repositories {
        google()
        jcenter()
        maven { url 'https://jitpack.io' }
    }

    buildscript {
        repositories {
            google()
            jcenter()
        }
    }

    dependencyUpdates.resolutionStrategy {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm', 'preview'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }

    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'maven-publish'

def getArtifactId = { ->
    return "rxandroidble"
}

def getArtifactVersion = { ->
    return VERSION_NAME
}

publishing {
    publications {
        app(MavenPublication) {
            groupId 'com.github.proxyco'
            artifactId getArtifactId()
            version getArtifactVersion()

            artifact('./rxandroidble/build/outputs/aar/rxandroidble-debug.aar') {}
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/proxyco/RxAndroidBle")
            credentials {
                username = System.getenv("GPR_USER")
                password = System.getenv("GPR_API_KEY")
            }
        }
    }
}